
package mem;
    typedef enum {
        BYTE, WORD, DWORD
    } mem_width_t;
endpackage

interface memory_bus();
    logic[31:0] addr;
    logic[31:0] write_data;
    logic dispatch_write;
    logic dispatch_read;
    mem::mem_width_t mem_width;
    
    logic[31:0] read_data;
    logic busy;

    modport MEMORY_SYSTEM (
        input addr, write_data, dispatch_read, dispatch_write, mem_width,
        output read_data, busy
    );
    modport CONSUMER (
        output addr, write_data, dispatch_read, dispatch_write, mem_width,
        input read_data, busy
    );
endinterface

module mmio_mappings(
    input logic[31:0] addr,

    output logic program_mem_enable,
    output logic ram_enable,
    output logic frame_buffer_enable,
    output logic io_enable,
    output logic[15:0] real_addr
);
    logic[1:0] mem_sel;
    always_comb begin
        mem_sel = addr[17:16];
        real_addr = addr[15:0];

        program_mem_enable = mem_sel == 0;
        ram_enable = mem_sel == 1;
        frame_buffer_enable = mem_sel == 2;
        io_enable = mem_sel == 3;
    end
endmodule

module memory_system (
    input wire clk_in,
    input wire rst_in,

    memory_bus bus,
    frame_buffer_bus fb_bus,
    sys_io_bus io_bus,
    program_memory_bus program_mem_bus
);
    logic[31:0] addr_latched;
    logic[31:0] data_latched;
    logic[2:0] bytes_to_send;

    logic[7:0] ram_data_out;
    logic[7:0] counter;
    logic we;
    // write enables
    logic[15:0] real_addr;
    logic is_io_addr, is_frame_buffer_addr, is_ram_addr, is_program_addr;
    mmio_mappings mmio(
        .addr(addr_latched),
        .real_addr(real_addr),
        .program_mem_enable(is_program_addr),
        .ram_enable(is_ram_addr),
        .frame_buffer_enable(is_frame_buffer_addr),
        .io_enable(is_io_addr)
    );
    
    // io bus helpers
    logic[31:0] io_read_data;
    assign io_read_data = real_addr < 4 ? {
        8'h00,
        io_bus.controller.joystick_x,
        io_bus.controller.joystick_y,
        io_bus.controller.buttons
    } >> 8 * (real_addr) : 32'hF0F055F0;

    enum {
        WAITING, READ_MEM, WRITE_MEM
    } state;

    assign bus.busy = (state != WAITING);

    always_ff @ (posedge clk_in) begin
        if (rst_in) begin
            bus.read_data <= 0;
            counter <= 0;
            we <= 0;
            state <= WAITING;
        end
        else begin
            if (we) begin
                we <= 0;
            end
            case (state)
            READ_MEM: begin
                if (is_io_addr) begin
                    bus.read_data <= io_read_data;
                    state <= WAITING;
                end
                else if (is_program_addr) begin
                    if (counter + 1 == 100) begin // wait 2 cycles
                        // the program instruction ram reads 4 byte instructions,
                        // ignoring the bottom 2 bits, so we offset based on address
                        // we also dont have to worry about partials, since riscv requires aligned access
                        //bus.read_data <= (program_mem_bus.instr_b >> (8 * real_addr[1:0]));
                        //bus.read_data <= real_addr + 256;
                        if (real_addr < 256) begin
                            bus.read_data <= 16'hF8_1F;
                        end
                        else begin
                            bus.read_data <= 16'hFF_FF; //(program_mem_bus.instr_b >> (8 * real_addr[1:0]));
                        end
                        state <= WAITING;
                    end
                    counter <= counter + 1;
                end
                else if (is_frame_buffer_addr) begin
                    // this should not be used in practice
                    if (counter + 1 == 4) begin // wait 4 cycles
                        bus.read_data <= fb_bus.debug_read;
                        state <= WAITING;
                    end
                    counter <= counter + 1;
                end
                else if (is_ram_addr) begin
                    if (counter + 1 == 3) begin // wait 2 cycles
                        counter <= 0;
                        if (bytes_to_send != 1) begin
                            bytes_to_send <= bytes_to_send - 1;
                        end
                        else begin
                            state <= WAITING;
                        end
                        bus.read_data <= {bus.read_data[23:0], ram_data_out};
                    end
                    counter <= counter + 1;
                end
            end
            WRITE_MEM: begin
                if (is_ram_addr) begin
                    if (counter == 2) begin // wait 2 cycles
                        counter <= 0;
                        if (bytes_to_send != 1) begin
                            bytes_to_send <= bytes_to_send - 1;
                            data_latched <= (data_latched >> 8);
                            we <= 1;
                        end
                        else begin
                            state <= WAITING;
                        end
                    end
                    counter <= counter + 1;
                end
                else begin
                    // we cant write to anything else
                    state <= WAITING;
                end
            end
            WAITING: begin
                counter <= 0;
                addr_latched <= bus.addr;
                data_latched <= bus.write_data;
                case (bus.mem_width)
                    mem::BYTE: bytes_to_send <= 1;
                    mem::WORD: bytes_to_send <= 2;
                    mem::DWORD: bytes_to_send <= 4;
                endcase
                if (bus.dispatch_read) begin

                    state <= READ_MEM;
                end
                else if (bus.dispatch_write) begin
                    state <= WRITE_MEM;
                    we <= 1;
                end
            end
            endcase
        end
    end

    // THE REST OF THIS IS INTERACTING WITH THE MEMORY
    // program memory.
    always_comb begin
        program_mem_bus.addr_b = real_addr;
    end

    // frame buffer. 1 cycle write
    parameter FB_SWAP_ADDR = 16'hFF_FF;
    always_comb begin
        fb_bus.write_clk = clk_in;
        fb_bus.write_addr = real_addr >> 1;
        // mem width implicitly word size
        fb_bus.write_data = data_latched[15:0];

        fb_bus.write_enable = we && is_frame_buffer_addr && (real_addr != FB_SWAP_ADDR);
        fb_bus.swap_buffer = we && is_frame_buffer_addr && (real_addr == FB_SWAP_ADDR);
    end

    // internal ram. 2 cycle read, 1 cycle write
    xilinx_single_port_ram_read_first #(
        .RAM_WIDTH(8),                      // Specify RAM data width
        .RAM_DEPTH(64*1024),                      // Specify RAM depth (number of entries)
        .RAM_PERFORMANCE("HIGH_PERFORMANCE") // Select "HIGH_PERFORMANCE" or "LOW_LATENCY" 
    ) ram (
        .addra(real_addr),        // Address bus, width determined from RAM_DEPTH
        .dina(data_latched[7:0]),      // RAM input data, width determined from RAM_WIDTH
        .wea(we && is_ram_addr),  // Write enable
        .douta(ram_data_out),      // RAM output data, width determined from RAM_WIDTH

        .clka(clk_in),       // Clock
        .ena(1),         // RAM Enable, for additional power savings, disable port when not in use
        .rsta(rst_in),       // Output reset (does not affect memory contents)
        .regcea(1)   // Output register enable
    );

endmodule